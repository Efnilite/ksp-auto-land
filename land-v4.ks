RCS ON.

CLEARSCREEN.

LOCK STEERING TO RETROGRADE.

PRINT "LOCKING STEERING".

WAIT UNTIL ALTITUDE < 35_000.

AG1 ON.
BRAKES ON.

PRINT "BRAKING".

WAIT UNTIL ALTITUDE < 10000.

// CALCULATE AMOUNT OF TIME REQUIRED AT 100% THROTTLE TO GO TO 0 SPEED
// EVERY ITER IMPROVES ESTIMATE UNTIL ABSOLUTELY REQUIRED TO BURN.

SET START_ALTITUDE TO -1.

UNTIL ALTITUDE < START_ALTITUDE {
    SET M TO MASS * 1000. // KSP RETURNS IN TONNES. WHY?
    SET MAXF TO MAXTHRUST * 1000. // KSP IN kN. WHY?

    SET MAX_VERT_DECEL TO 0.92 * MAXF/M. // todo fix

    PRINT MAX_VERT_DECEL.

    SET BOOST_TIME TO -VERTICALSPEED / MAX_VERT_DECEL.
    SET START_ALTITUDE TO 0.5 * MAX_VERT_DECEL * BOOST_TIME^2 + 30. // HEIGHT OF ROCKET

    PRINT BOOST_TIME.
    PRINT START_ALTITUDE.
}

WAIT UNTIL ALTITUDE < START_ALTITUDE.

PRINT "INITING SUICIDE BURN".

LOCK THROTTLE TO 1.0.
UNLOCK STEERING.

WAIT UNTIL VERTICALSPEED > -30.

PRINT "FINAL DESCENT".

TOGGLE AG2.
TOGGLE BRAKES.

LOCK THROTTLE TO 0.5.
LOCK STEERING TO RETROGRADE.

WHEN VERTICALSPEED > -1 THEN {
    LOCK THROTTLE TO 0.4.

    IF ALTITUDE > 2 {
        RETURN TRUE.
    }
    RETURN FALSE.
}

WHEN VERTICALSPEED < -4 THEN {
    LOCK THROTTLE TO 0.6.

    IF ALTITUDE > 2 {
        RETURN TRUE.
    }
    RETURN FALSE.
}

WAIT UNTIL ALTITUDE < 2.

PRINT "LAND".

UNLOCK STEERING.
LOCK THROTTLE TO 0.
SAS OFF.
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
